buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = VERSION_NAME

def DEFAULT_COMPILE_SDK_VERSION = 26
def DEFAULT_TARGET_SDK_VERSION = 26
def DEFAULT_SUPPORT_LIBRARY_VERSION = '26.0.0'

android {

    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        consumerProguardFiles 'proguard.txt'

        buildConfigField "String", "SXFANALYSIS_VERSION", "\"${version}\""

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources"
            textOutput "stdout"
            textReport true
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// Note- *all* dependencies are marked as optional in our final
// pom! If you need a transitive dependency in the library, you'll
// have to change the bit in uploadArchives that marks all dependencies as optional.
dependencies {
    def supportLibraryVersion = project.hasProperty('supportLibraryVersion') ? project.supportLibraryVersion : DEFAULT_SUPPORT_LIBRARY_VERSION
    compile "com.android.support:support-annotations:$supportLibraryVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

//apply from: rootProject.file('maven.gradle')
